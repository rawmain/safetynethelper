apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'com.scottyab.safetynet.sample'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 7
        versionName "0.7"
    }

    //check if SAFETY_NET_DEVICE_VERIFICATION_API_KEY is defined in gradle.properties (this is so the key is not in github)
    String GOOGLE_VERIFICATION_API_KEY = "";
    if (project.hasProperty("SAFETY_NET_DEVICE_VERIFICATION_API_KEY")) {
        GOOGLE_VERIFICATION_API_KEY = SAFETY_NET_DEVICE_VERIFICATION_API_KEY
    }

    //check if the keystore details are defined in gradle.properties (this is so the key is not in github)
    if (project.hasProperty("SAFETY_NET_FILE")) {
        signingConfigs {
            //from gradle.properties
            release {
                storeFile file(SAFETY_NET_FILE)
                keyAlias SAFETY_NET_KEY
                storePassword SAFETY_NET_STORE_PASS
                keyPassword SAFETY_NET_KEY_PASS
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            minifyEnabled false
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("SAFETY_NET_FILE")) {
                signingConfig signingConfigs.release
            }
        }
        release {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            minifyEnabled false
            if (project.hasProperty("SAFETY_NET_FILE")) {
                signingConfig signingConfigs.release
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    implementation project(':safetynetlib')

    //testing the jitlab dependancy
    //compile 'com.github.scottyab:safetynethelper:0.3.0@aar'
    
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.huawei.hms:base:4.0.3.301'
}

apply plugin: 'com.huawei.agconnect'